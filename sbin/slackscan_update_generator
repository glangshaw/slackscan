#!/bin/awk -f
#
#  slackscan_update_generator.awk,
#  Version 2.4
#
#  This awk-script takes a list of Slackware packages on stdin and
#  converts them to a stream of pkgtools updatepkg commands.  It is
#  intended for use with the output generated with slackscan's -i and
#  -u options.
#

BEGIN {
    FS="/"
    url = 0

    print "# Package update commands generated by slackscan_update_generator.awk"
    print "#"
}

/^https?:\/\/.*\/[^/]+-[^-/]+-[^-/]+-[0-9]+[^-/.]*\.t[^-/.]z$/ {
    print "curl --retry 3 -O '"$0"'"
    print "curl --retry 3 -O '"$0".asc'"
    url = 1
}

/^(.*\/)*[^/]+-[^-/]+-[^-/]+-[0-9]+[^-/.]*\.t[^-/.]z$/ {
    if ( url == 1 )
        f = $NF
    else
        f = $0

    print "gpgv --keyring pubring.gpg \\"
    print "  '"f".asc' \\"
    print "  '"f"' \\"
    print "&& upgradepkg --install-new '"f"' "
    print ""

    url = 0
}
